plugins {
    id 'maven-publish'
    id 'org.ajoberstar.git-publish' version '4.1.1'
    id 'signing'
}

version '1.0.0'
archivesBaseName = "brainfrick-runtime"

repositories {
    mavenCentral()
}

dependencies {
    compileOnly 'org.jetbrains:annotations:23.0.0'
}

repositories {
    mavenCentral()
}

artifacts {
    archives javadocJar, sourcesJar
}

javadoc {
    title = "Brainfrick Runtime - v${version}"
}

gitPublish {
    repoUri = "https://github.com/lukebemish/brainfrick.git"

    branch = 'gh-pages'

    repoDir = file("$buildDir/gitPublish")
    contents {
        from(javadoc) {
            into 'apidoc/runtime'
            exclude 'stylesheet.css'
        }
    }
    preserve {
        include '**/*'
        exclude {FileTreeElement file ->
            file.relativePath.pathString.startsWith("apidoc/runtime/") &&
                    !(file.relativePath.pathString.startsWith('apidoc/runtime/stylesheet.css'))
        }
    }
    commitMessage = 'Updating javadocs - runtime'
}

task pushJavadoc() {
    doLast {
        logger.warn "Remember to update pages!"
    }
}

publish.dependsOn pushJavadoc
pushJavadoc.dependsOn gitPublishCommit

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = "Brainfrick Runtime"
                packaging = 'jar'
                artifactId = 'brainfrick-runtime'
                description = 'Classes used at runtime by brainfrick, a modified brainfuck able to be compiled for or run on the JVM.'
                url = 'https://github.com/lukebemish/brainfrick'
                inceptionYear = '2022'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'lukebemish'
                        name = 'Luke Bemish'
                        email = 'lukebemish@gmail.com'
                        url = 'https://github.com/lukebemish/'
                    }
                }
                scm {
                    connection='scm:git:git://github.com/lukebemish/brainfrick.git'
                    developerConnection='scm:git:ssh://github.com/lukebemish/brainfrick.git'
                    url='https://github.com/lukebemish/brainfrick'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = project.ossrhUsername
                password = project.ossrhPassword
            }
        }
    }
}

if(project.hasProperty("signing.gnupg.keyName")) {
    signing {
        useGpgCmd()
        sign publishing.publications.maven
    }
}
