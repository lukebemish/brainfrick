plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'java-gradle-plugin'
    id 'groovy'
    id 'maven-publish'
    id 'org.ajoberstar.git-publish' version '4.1.1'
    id 'com.gradle.plugin-publish' version '1.0.0'
}

version '1.0.5'
archivesBaseName = "brainfrick-plugin"

repositories {
    mavenCentral()
}

configurations {
    shade
    compileOnly.extendsFrom shade
}

dependencies {
    shadow localGroovy()
    shadow gradleApi()

    shadow(implementation 'org.apache.groovy:groovy:4.0.4')
    shadow(implementation 'org.ow2.asm:asm:9.3')
    shadow(implementation 'org.apache.commons:commons-text:1.9')
    shadow(implementation 'org.antlr:antlr4-runtime:4.10.1')
    compileOnly 'org.jetbrains:annotations:23.0.0'

    shade project(':compiler')
}

tasks.shadowJar {
    classifier = ''
    configurations = [project.configurations.shade]
    dependencies {
        include(project(":compiler"))
        include(project(":runtime"))
    }
}

sourcesJar {
    from(project(":compiler").sourceSets.main.allSource)
    from(project(":runtime").sourceSets.main.allSource)
}

pluginBundle {
    website = 'https://github.com/lukebemish/brainfrick'
    vcsUrl = 'https://github.com/lukebemish/brainfrick'
    tags = ['brainfrick']
}

jar.finalizedBy(shadowJar)

gradlePlugin {
    plugins {
        simplePlugin {
            id = 'io.github.lukebemish.brainfrick'
            displayName = "Brainfrick Plugin"
            description = "Plugin supporting compilation of brainfrick code and brainmaps"
            implementationClass = 'io.github.lukebemish.brainfrick.plugin.gradle.BrainfrickBasePlugin'
        }
    }
}

javadoc {
    title = "Brainfrick Gradle Plugin - v${version}"
}

publish.dependsOn rootProject.prepJavadocs



